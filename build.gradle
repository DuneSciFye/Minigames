plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.3"
}

group = 'me.dunescifye'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.onarandombox.com/content/groups/public/" } // Multiverse Core
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT"
    compileOnly 'com.onarandombox.multiversecore:multiverse-core:4.3.12' // Multiverse Core
    implementation "dev.jorel:commandapi-bukkit-shade:10.0.0" // CommandAPI
    compileOnly files('src/main/resources/SCore.jar') // SCore
}

shadowJar {
    relocate 'dev.jorel.commandapi', 'me.dunescifye.libs.commandutils.commandapi'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
